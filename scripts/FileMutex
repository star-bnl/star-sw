#!/bin/csh
# * $Id: FileMutex,v 1.7 2006/07/27 19:06:16 fine Exp $
# Author: Valeri Fine fine@bnl.gov
# Date:   25.06.2006
#
# The Dijkstra ( binary only) semaphore (pseudo) implpementation to synchronize file access
# See: http://en.wikipedia.org/wiki/Semaphore_(programming) 
#-----------------------------------------------
# P(Semaphore s) { await s > 0, then s := s-1; /* must be atomic once s > 0 is detected */ }
#
# V(Semaphore s) {   s := s+1;   /* must be atomic */ # }
#
# Init(Semaphore s, Integer v) {   s := v;  }
# 
# where s ::= 0 | 1 ( for binary sempahore )
#         s := 0 means the $SEMAPHORE_MUTEX_FILE "unlock" file is to be removed with the shell "rm" command
#              1 means the $SEMAPHORE_MUTEX_FILE "unlock" file is to be created with the shell "touch" command
#
# Note: Strickly say, the file system doesn't guaranty either shell command is "atomic" though
#       (see:  "http://en.wikipedia.org/wiki/Atomic_%28computer_science%29"   )
#
#--------------------------------------------------------------------------------------------
# Usage: FileMutex ["Lock" | "Unlock" | "Init" | "Help"]  [$REQUESTID] [$MUTEXDIR] [$PROCESSID]
# where the $REQUESTID variable is to be generated by  SUMS
#--------------------------------------------------------------------------------------------
#
# It is assumed the combination [$REQUESTID] + [$PROCESSID] is unique for
# all processes you want to share your common resource among;
# and the all cooperating processes provide one and the same value for [$REQUESTID]
# Both conditions above are met automatically if the jobs are submitted via SUMS
#
#-----------------------------------------------
set STATUS=0
if (  $1 != "" ) then
  set SEMAPHORE_TYPE=$1
  shift
else
  # Init semaphiore "by default"
  set SEMAPHORE_TYPE="Init"
endif

# use the REQUESTID "by default"
if (  $1 != "" ) then
  set MUTEX_ID=$1
  shift
else 
   if ( $?REQUESTID ) then
     set MUTEX_ID=$REQUESTID
   else
     echo " The REQUESTID parameter of the script was not defined."
     echo " This may entail the synch problem. Fix you script, please"
     set MUTEX_ID=REQUESTID
   endif
endif

# The mutex directory should be defined with the absolute path
if (  $1 != "" ) then
  set MUTEX_DIR=`echo $1`
  shift
else
  set MUTEX_DIR=`echo $HOME/Mutex`
  mkdir -p $MUTEX_DIR >&/dev/null
  echo  " MUTEX_DIR                 $MUTEX_DIR"
endif

# The mutex comment
if (  $1 != "" ) then
  set MUTEX_COMMENT=MUTEX_COMMENT
else
  if ($?PROCESSID ) then
     set MUTEX_COMMENT=$PROCESSID
   else
     set MUTEX_COMMENT=PROCESSID
   endif
endif


set INIT_MUTEX_FILE_COMPLETE=$MUTEX_DIR/$MUTEX_ID.init
set SEMAPHORE_MUTEX_FILE=$MUTEX_DIR/$MUTEX_ID.unlock

#echo ------------------------------------------------------
#echo  SEMAPHORE_TYPE            $SEMAPHORE_TYPE 
#echo  MUTEX_ID                  $MUTEX_ID 
#echo  MUTEX_DIR                 $MUTEX_DIR 
#echo  MUTEX_COMMENT             $SEMAPHORE_TYPE 
#echo  INIT_MUTEX_FILE_COMPLETE  $INIT_MUTEX_FILE_COMPLETE 
#echo  SEMAPHORE_MUTEX_FILE      $SEMAPHORE_MUTEX_FILE
#echo ------------------------------------------------------

switch ( $SEMAPHORE_TYPE )
  case "Lock" :
   # -----  P-semaphore (Lock) ------------------
   while (1)
     if ( ! -e $INIT_MUTEX_FILE_COMPLETE ) then
        echo The \"FileMutex Init\" has not been called yet. Please fix your script!
        set STATUS=1
        breaksw
     endif
     rm $SEMAPHORE_MUTEX_FILE >&/dev/null
     if ( ! $status ) break
     echo "Waiting the critical section access permission . . . "
     sleep 20  
   end
   breaksw
      
  case "Unlock" :
  # -----  V-semaphore (Unlock) -----------------
    if ( ! -e $INIT_MUTEX_FILE_COMPLETE ) then
        echo The \"FileMutex Init\" has not been called yet. Please fix your script!
        set STATUS=1
        breaksw
    endif
    touch $SEMAPHORE_MUTEX_FILE
    if (  -e $SEMAPHORE_MUTEX_FILE ) then
       echo "Granted access for others!"
    endif
    breaksw
    
  case "Init" :
  # -----  Init-semaphore -----------------------
    mkdir -p $MUTEX_DIR >&/dev/null
    if ( ! -e $INIT_MUTEX_FILE_COMPLETE ) then
    #
    #  The critical section initialization
    # ---- V-semaphore (unlock) ---------------
       if ( ! -e $SEMAPHORE_MUTEX_FILE ) then
          touch  $SEMAPHORE_MUTEX_FILE 
          touch  $INIT_MUTEX_FILE_COMPLETE
       endif
    endif
    breaksw

  default:
    set STATUS=1
    # -----  Help message -----------------------
    echo ""
    echo 'Usage: FileMutex ["Lock" | "Unlock" | "Init" | "Help" ] [$REQUESTID] [$MUTEXDIR] [$PROCESSID]'
    echo '-----  default parameters:  FileMutex Init $REQUESTID $HOME/Mutex  $PROCESSID'
    echo ""
    echo '       Your script should contain:'
    echo ""
    echo '          1. One invocation of the "FileMutex [Init]" before any "Lock"/"Unlock" function'
    echo '          2. Calling "FileMutex Lock"  twice without any "FileMutex Unlock" in between'
    echo '             leads to the "dead lock"'
    echo ""
    echo '        Report the problems and communicate your suggestions'
    echo '        via "STAR Bug" web page http://www.star.bnl.gov/rt2/'
    echo ""
    breaksw
  
endsw

exit $STATUS
