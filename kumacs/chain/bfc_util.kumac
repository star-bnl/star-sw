 
* 2Jul98 K. Turner - tpc code now requires geometry so make it default
*                    in domain
* 3Jul98 K. Turner - changed tpc kumacs so that pam kumacs are called directly
* 7Jul98 K. Turner - changed to use gstar stuff
* 9Jul98 K. Turner - changed to use new tpc cluster and track eval for slow sim
* 9Jul98 K. Turner - changed to use tfc_run#run_newtab - can set adcxyzon=1
* 10Jul98 K. Turner - changed to use new version of global pam kumacs
* 11Jul98 K. Turner - changed to used tfc for adcxyz table
* 11Jul98 K. Turner - changed to load gstar more standard way 
*                   - changed inputs to bfc_start!
* 13Jul98 K. Turner - change to separate out parts of setup 
* 21Jul98 K. Turner - removed gethij & gst - not needed
* 21Jul98 K. Turner - changed to separate out the gstar & g2t parts
* 22Jul98 K. Turner - fixed to pick up correct gstar files
* 27Jul98 K. Turner - fixed to include all domains in bfc_defaults 
* 27Jul98 K. Turner - allow to run gstar from input txt file
* 28Jul98 K. Turner - separate bfc_defaults and bfc_load to 
*                      kumacs/util/setup_defaults and load_libraries
* 1Aug98  K. Turner - tid now separated out of tpt
* 3Aug98  K. Turner - use new detp geometry command instead of bfc_gstar_setup
* 7Aug98  K. Turner - removed unneeded user/input under evgin in bfc_process 
* 11Aug98 K. Turner - now make split_off default for running gstar 
* editted 8/12/98 K. Turner - more comments; combine geomtype+phystype into
*                             gstar_settings; use gstar_settings=field_only
*                             if not running gstar
* editted 8/12/98 K. Turner - added new modules to chain:
*                               ==> chain name/module
*                                    evr       evr
*                                    ddedx     dst_dedx_filler
*                                    dmsft     dst_monitor_soft_filler
*                                    dmhrd     not available yet
*                                    desum     fill_dst_event_summary
*                                    drsum     fill_dst_run_summary
*                                    emc       ems_interface2
*                                    jet       emc_egrid,emc_erj
*                                    ctf       cts,ctu
*                                    mwc       mws,mwu
*                                    trg       l3cl,ftf_tpc - won't work yet
* 
* editted 8/12/98 K. Turner - added new module to chain: 
*                               ==> chain name/module
*                                     ffs - ftpc fast sim
*                                     fss - ftpc slow sim
*                                     fcl - ftpc clustering
*                                     fpt - ftpc tracking
*                                     fte - ftpc track evaluator
*                                    
* editted 8/24/98 K. Turner - put in changes from Lanny:
*                               ==> chain name/module
*                                   dpnt  - dst point filler
*                                   desum - dst event summary - now works
* 
* editted 8/24/98 K. Turner - new/changed modules in chain
*                               ==> chain name/module
*                                     fpt_s - ftpc tracking - slow sim
*                                     fte_s - ftpc track evaluator - slow sim
*                                     fpt_f - ftpc tracking - fast sim
*                                     fte_f - ftpc track evaluator - fast sim
*                           - evr MUST be called before ev0
*
* editted 8/25/98 K. Turner - in (evin), right after reading in tables,
*                             rmdir event/dst - mismatch between old & new 
*                             directory structure
* 
* editted 8/26/98 K. Turner - in (evin), rm run/params/svt tables that
*                             are in wrong place and not needed
* editted 8/26/98 K. Turner - fixed (fzin) so it would work 
* editted 8/26/98 K. Turner - now use par2mem to read in svt & tpc run/params
*                             tables so had to change init kumacs for svt,tpc
*                           - remove rm and rmdir (see few days ago);  just 
*                             leave old unused tables in
*                           - write out information to log file from (rg2t)
* editted 8/27/98 K. Turner - now get global tables in run/params/global
*                             from par2mem, so have to change calls to
*                              global init kumacs
*                             
* editted 8/28/98 K. Turner - changed names of macros in init_dst
* editted 8/31/98 K. Turner - changes:
*                           ==> chain name/module
*                           - added new module into chain:
*                                     dehd - dst event header table 
*                           - got module working:
*                                     drsum - fill_dst_run_summary
*                           - modified recently:
*                                     dmsft - dst_monitor_soft_filler
*                                     desum - fill_dst_event_summary
* editted 8/31/98 K. Turner - write out run directories at finish
* editted 8/31/98 K. Turner - broke up event and run summary and header kumacs
*                             and now add new module to chain:
*                                 drhd - dst run header table
* 
* editted 9/1/98 K. Turner - increment counters nevdsttot,nevdstgood 
*                            defined in setup.kumac
* editted 9/1/98 K. Turner - implemented drsum in bfc_finish
* editted 9/9/98 K. Turner - now writes out run tables in bfc_finish
*                              ONLY if drhd or drsum is in chain
* editted 9/10/98 K. Turner - added new chain variable: dout
*                             which writes out run/dst and 
*                             event/data/global/dst directories to a file
* editted 9/11/98 K. Turner - now write out run tables in bfc_finish
*                             only if DRSUM is in chain
*                           - in evin: check  error if from 
*                             read_table_from_file
*                             and skip out if vector is set 
*                           - change emc to ems in chain 
* editted 9/14/98 K. Turner - new chain variable dstin to read in dst
*                             file and put tables in correct directories
* ************************************************************
*                     BFC_UTIL.KUMAC
*************************************************************
******************************************************************
 macro bfc_start _ 
   domain='geometry tpc svt global' _
   chain=' ' _
   gstar_settings=' ' 

* import global macros
   macro/global/import *

*  THESE NEXT 4 MUST BE DONE IN THIS ORDER!!!!
   message ' *** SET GEOMETRY in gstar:  -->   ' [gstar_settings]
   message '        !!! split_off is hardwired into this kumac !!! '
   detp geometry [gstar_settings] split_off
   exec bfc_util#bfc_g2t_b4_load    [chain]
   if (STAF_STATUS(1).ne. STAFCV_OK) goto ABEND
   exec load_libraries#run          [domain]
   if (STAF_STATUS(1).ne. STAFCV_OK) goto ABEND
   exec bfc_util#bfc_g2t_after_load [chain]
   if (STAF_STATUS(1).ne. STAFCV_OK) goto ABEND
ABEND: 
return
 
*********************************************************************
*********************************************************************
**********************************************************************
 
macro bfc_init _
         chain=' g2tin svg tpg srs tfs sgr spr tpt tte_e svm egr ev0 evout' 
  
  macro/global/import *
    
 test = [chain]
 nw = $words([test])
 message ' !!! bfc_util#init ==> Number of words in chain = ' [nw]
 
 do i = 1, [nw]
   mod = $word([test],[i],1)
   message ' !!! bfc_util#init ==>  chain init for ' [mod] 
   vec/create v[mod]
   vec/create t[mod]
   CASE [mod] in
    (oldin) user/input dummy [input_file]
    (evgin) newfile hij_in [input_file] r; 
            user/input u [evgen]/particle.staf
    (gtxin) user/input dummy [input_file]
     (fzin)  
            gfile pz [input_file]
            tables ' ' [geant_Run]
            detp g2tm dete(cdet=SFSD).onoff=0 gttc.rdir=0
    (g2tin) 
	    exec input_g2t_file#init [input_file]
            if (STAF_STATUS(1).ne. STAFCV_OK) then
              message 'problem opening input file '
              goto EOF 
            endif
     (evin)
          exec read_table_from_file#init inev [input_file]
           if ($vexist(readprob).ne.0) then 
            message ' BFC_UTIL,init: ERROR - cannot open input file - quit '
            goto EOF
           endif
          exec read_table_from_file#read inev
           if ($vexist(readprob).ne.0) then 
            message ' BFC_UTIL,init: ERROR - cannot read input file - quit '
            goto EOF
	  endif
    (dstin) 
	  exec read_table_from_file#init ind [input_file]
	   if ($vexist(readprob).ne.0) then 
            message ' BFC_UTIL,init: ERROR - cannot open dstin file - quit '
            goto EOF
	   endif
	  exec read_table_from_file#read ind [run]
           if ($vexist(readprob).ne.0) then 
            message ' BFC_UTIL,init: ERROR - cannot read dstin file - quit '
            goto EOF
	   endif  
     (esin)
          dio/newfilestream inev [input_file] r
          cd [run]
          dio/stream/getevent inev
      (svg) 	  
           exec init_svg#svg_am
      (tpg) 
           exec tpg_init#init
           exec tpg_init#tpg_main
      (sss)
 	   exec init_sss#read
           exec init_ssf#read
	   exec init_sss#init
	   exec init_ssf#init
      (srs) 
           exec create_tables_svt#srs
	   exec init_srs#init
      (tfs) 
         exec create_tables_tpc#tcl_fast
         exec tcl_init#init
	 exec tfs_init#init
      (tss) 
	 exec create_tables_tpc#tss [tpc_sector_first] [tpc_sector_last]
         exec tss_init#init [tpc_sector_first] [tpc_sector_last]
      (tfc) 
	 exec create_tables_tpc#tfc
      (tcl)
         exec create_tables_tpc#tcl_slow
	 exec tcl_init#init
      (tpt)
	 exec create_tables_tpc#tpt
	 exec tpt_init#init
    (tte_t) 
	 exec create_tables_tpc#tpt
	 exec tpt_init#init
    (tte_e)
         exec create_tables_tpc#tte
	 exec tte_init#init
      (tid) 
         exec tid_init#init
      (stk)
	 exec create_tables_svt#sgr
         exec create_tables_svt#stk
         exec init_vertices#init
	 exec init_sgr#init
	 exec init_stk#init
      (sgr) 
	 exec create_tables_svt#sgr
         exec create_tables_svt#stk
         exec init_vertices#init
         exec init_sgr#init
	 exec init_stk#init 
      (spr) 
         exec init_spr#init
      (ffs) 
	    exec create_tables_ftpc#ffs_tables
	    exec ffs_init#init
      (fss) 
	    exec create_tables_ftpc#fss_tables
	    exec fss_init#init
      (fcl) 
	    exec create_tables_ftpc#fcl_tables
	    exec fcl_init#init
    (fpt_f)
	    exec create_tables_ftpc#fpt_tables
	    exec fpt_init#init
    (fte_f)
	    exec create_tables_ftpc#fte_tables
            exec fte_init#init
    (fpt_s)
            exec create_tables_ftpc#fpt_tables
            exec fpt_init#init
    (fte_s)
            exec create_tables_ftpc#fte_tables
            exec fte_init#init	    
      (svm) 
            exec create_tables_global#svm
            exec init_svm#init
      (evr) exec init_evr#init
      (egr) 
	    exec create_tables_global#egr
	    exec init_egr#init
      (ev0) exec create_tables_global#ev0 
            exec init_ev0#init
            exec init_magf#init
    (ev0_e) exec create_tables_global#ev0_eval2
      (ctf) exec create_tables_ctf#init
            exec ctg_init#read
            exec cts_init#cts_init
      (mwc) exec mwg_init
	    exec mws_init
	    exec create_tables_mwc#init
      (trg) exec l3cl_init
	    exec l3t_init
	    exec create_tables_l3
      (ems) exec create_tables_emc#ems
	    exec init_ems#read
	    exec init_ems#init
      (jet) exec create_tables_emc#jet
	    exec init_jet#init
    (ddedx) exec create_tables_global#dst_dedx_filler
     (dpnt) exec create_tables_global#dst_point_filler
    (dmsft) exec create_tables_global#dst_monitor_soft_filler
            exec create_tables_ctf#init
    (dmhrd) 
    (desum) exec create_tables_global#fill_dst_event_summary
    (drsum) exec create_tables_global#fill_dst_run_summary
     (dehd) exec create_tables_global#fill_dst_event_header
     (drhd) exec create_tables_global#fill_dst_run_header
	    exec init_dst#init_rhead
    (fzout) user/output o [output_fzd_file]
    (evout)
            if output_xdf_file="" then
              message "empty output file"
            else
	      exec write_table_to_file#init outfile [output_xdf_file]
              if (STAF_STATUS(1).ne. STAFCV_OK) then
                message "Cannot open output file"
                goto eof
              endif
              exec write_table_to_file#write outfile [run]         
            endif
    (dout)  
            if output_dst_file="" then
              message "empty dst output file"
            else
	      exec write_table_to_file#init dstout [output_dst_file]
              if (STAF_STATUS(1).ne. STAFCV_OK) then
                message "Cannot open dst output file"
                goto eof
              endif
              exec write_table_to_file#write dstout [run_dst]         
            endif
    (esout)
            if output_xdf_file="" then
              message "empty output file"
            else
              dio/newfilestream outfile [output_xdf_file] w
              if (STAF_STATUS(1).ne. STAFCV_OK) then
                message "Cannot open output file"
                goto eof
              endif
              cd [run]
              dio/stream/puteven outfile
            endif
      (*)   message " !!! bfc_util: No initialization for package " [mod]
    ENDCASE
    if (STAF_STATUS(1).ne. STAFCV_OK) goto ABEND
    
  enddo
EOF:
ABEND:
 
 return
 
**********************************************************************
**********************************************************************
**********************************************************************

 macro bfc_process _
      chain=' g2tin svg tpg srs tfs sgr spr tpt tte_e svm egr ev0 evout'_
   
  macro/global/import *

  test = [chain]
  nw = $words([test])

  nevdsttot = [nevdsttot]+1

  do i = 1, [nw]
   mod = $word([test],[i],1)
   message ' !!! bfc_util#process ==> chain process for ' [mod] 
   CASE [mod] in 
    (oldin) nextl 1
    (evgin) cd [event]; 
            getev hij_in
     (g2tin) exec input_g2t_file#read
            if (staf_status(1).ne.STAFCV_OK) goto EOF
     (gtxin) nextl 1
     (evin) exec read_table_from_file#read inev
           if ($vexist(readprob).ne.0) then 
            message ' BFC_UTIL,process: ERROR - cannot read input file - quit '
            goto EOF
           endif
            ls [run_dst]/run_summary
            if (staf_status(2).eq.STAFCV_OK) then
*              message ' *** BFC_PROCESS: run summary table exists! '
              rowcount [run_dst]/run_summary	    
	      if (staf_result(1).ne.0) then
*                message ' *** BFC_PROCESS: run summary table has entries! '
                tdm/table/cell/getvalue 'run/dst/run_summary[0].n_events_tot' 
                if (staf_result(1).ne.0) then 
	            vec/create havedstrunsum
*	            message ' *** BFC_PROCESS: final run_summary table!'
	            goto FINISH
	        endif
              endif
            endif
   (dstin) exec read_table_from_file#read ind [event]/data/global
           if ($vexist(readprob).ne.0) then 
            message ' BFC_UTIL,process: ERROR - cannot read dstin file - quit '
            goto EOF
           endif
	   ls [dst]/run_summary
            if (staf_status(2).eq.STAFCV_OK) then
* Kathy 9/14
* - have run_summary & run_header tables in wrong directory at this
*   point and now must delete initial ones,  move theses and then
*   go to bfc_finish	       
	      vec/create havedstrunsum
	      rm [run_dst]/run_header
	      rm [run_dst]/run_summary
	      mv [dst]/run_header [run_dst]
	      mv [dst]/run_summary [run_dst]
	      goto FINISH	
            endif
     (esin) for dir in [evgen] [geantE] [raw_data] [data] [dst]
              cd [dir]; dio/stream/getevent inev 
              if (staf_status(1).ne.STAFCV_OK) goto EOF
            endfor
     (fzin) trig 1 
     (rgst) trig 1
     (rg2t) cd [geantE]
          for/call g2t
          cd /dui
          tdm/table/rowcount event/geant/Event/g2t_vertex
          no_vert=$format(staf_result(1),i10)
          tdm/table/rowcount event/geant/Event/g2t_track
          no_track=$format(staf_result(1),i10)
          string = '---<bfc>---rg2t: Event  No.vertices ='//no_vert//' No.Tracks = '//no_track
	  fmessage $quote([string])    [log_file]
          message $quote([string])
      (svg)    nextl 1
      (tpg)    nextl 1
      (sss)    exec run_sss
               exec run_ssf
      (srs)    exec run_srs
               if (staf_status(1) .ne. stafcv_ok) then 
                  message ' problem with run_srs'
		endif
      (tfs)    
               exec tfs_run#run
               if (staf_status(1) .ne. stafcv_ok) then 
                  message ' problem with tfs_run'
	       endif
      (tss)    
               exec tss_run#run [tpc_sector_first] [tpc_sector_last]
               if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tss_run'
               endif
      (tfc)    exec tfc_run#run_newtab [tpc_sector_first] [tpc_sector_last]
      (tcl)    
	       exec tcl_run#run_tcl [tpc_sector_first] [tpc_sector_last] 
               exec tcl_run#run_tph [tpc_sector_first] [tpc_sector_last]
               if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tpc_clustering_process_event'
               endif
    (tcl_e)    exec tte_run#run_tte_hit_match
       
      (stk)    exec run_stk#run
               if (staf_status(1) .ne. stafcv_ok) then
                 nextl 1;
               endif
      (sgr)    exec run_sgr#run
	       exec run_stk#run
               if (staf_status(1) .ne. stafcv_ok) then
                 nextl 1;
               endif
      (spr)    exec run_spr#run
      (tpt)    exec tpt_run#run
	       if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tpc_tracker_tpt'
               endif 
    (tte_t)    exec tte_run#run_tte_track
                 if (staf_status(1) .ne. stafcv_ok) then 
                   message ' problem with tpc_tracker_tte'
                 endif
      (tid)    exec tid_run#run_tde_new 
               if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tpc_tracker_tpt'
               endif
    (tte_e)    exec tte_run#run_tte
      (ffs) exec ffs_run#run 
      (fss) exec fss_run#run
      (fcl) exec fcl_run#run
    (fpt_f) exec fpt_run#run_fast_sim
    (fte_f) exec fte_run#run_fast_sim
    (fpt_s) exec fpt_run#run_slow_sim
    (fte_s) exec fte_run#run_slow_sim
      (svm)    exec run_svm#run_svm 	     |  TPC to SVT Matching 
      (evr)    exec run_evr#run
      (egr)    exec run_egr#egr	|  Event Global Refit
      (ev0)    exec run_ev0#ev0_am           |  V0 Finder 
    (ev0_e)    exec run_ev0#ev0_eval2  
      (ctf)    exec cts_run
               exec ctu_run 
      (mwc)    exec mws_run
	       exec mwu_run
      (trg)    exec l3cl_run
	       exec ftf_run
      (ems)    exec run_ems#ems_interface2  
      (jet)    exec run_jet#egrid
               exec run_jet#erj
     (dehd)    exec init_dst#init_ehead
    (ddedx)    exec run_dst#dst_dedx_filler
     (dpnt)    exec run_dst#point_filler
    (dmsft)    exec run_dst#dst_mon_soft
    (dmhrd)    nextl 1
    (desum)    exec run_dst#dst_event_summary
    (drsum)    exec run_dst#dst_run_summary
    (evout)    if [output_xdf_file]="" then
                 message "Empty output file"
               else
		 exec write_table_to_file#write outfile [event]
*                 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
              endif
    (dout)    if [output_dst_file]="" then
                 message "Empty dst output file"
               else
		 exec write_table_to_file#write dstout [dst]
*                 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT	
               endif
    (esout)    if [output_xdf_file]="" then
                 message "Empty output file"
               else
                 for dir in [evgen] [geantE] [raw_data] [data] [dst]
                   cd [dir];  dio/stream/putevent outfile
                 endfor
	       endif
      (*)      message " !!! bfc_util: No process stage for package " [mod]
      if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   ENDCASE


   if (STAF_STATUS(1).ne. STAFCV_OK) goto ABEND
	     
   sigma v[mod] = v[mod] + abs($cptime)
   sigma t[mod] = t[mod] + v[mod]
   string = '---<bfc>---'//[mod]//': Event  '//$format([ievent],i6)//$format(v[mod],f12.5)//$format(t[mod],f12.5)
   fmessage $quote([string])    [log_file]
    message $quote([string]) 
   vec/inp v[mod] 0.0
   
  enddo
  asu/malloc/stats 

  nevdstgood = [nevdstgood]+1

 ABEND:
 NEXT_EVENT:
 EOF:

 goto END
 
 FINISH:
 message ' BFC_UTIL,process: skip out because got to final run summary table'
 
 END:

 message ' BFC_UTIL,process: #events total,good = ', [nevdsttot], [nevdstgood]

return
 
**********************************************************************
**********************************************************************
**********************************************************************

macro bfc_finish _
   chain=' g2tin svg tpg srs tfs sgr spr tpt tte_e svm egr ev0 evout'
  
  macro/global/import *
  test = [chain]
  nw = $words([test])
  vec/cre totime r 0

  do i = 1, [nw]
    mod = $word([test],[i],1)
    message ' !!! bfc_util ==> finish for chain value: ' [mod]
    CASE [mod] in 
     (g2tin)  exec input_g2t_file#close
     (evin)  exec read_table_from_file#close inev 
     (dstin) exec read_table_from_file#close ind
     (rgst)  ge/rz/cd //PAWC; hi/list; | show histograms
*     (drhd)  vec/create randst
     (drsum) exec  finish_dst#dst_run_summary
             exec  run_dst#dst_run_summary
             vec/create randst
     (evout,esout) 
            if [output_xdf_file]="" then
                 message "Empty output file"
            else
              if ($vexist(randst).ne.0 .or. $vexist(havedstrunsum).ne.0) then
	        exec write_table_to_file#write outfile [run]
              endif
              exec write_table_to_file#close outfile;
            endif
      (dout) 
            if [output_dst_file]="" then
                 message "Empty dst output file"
            else
              if ($vexist(randst).ne.0 .or. $vexist(havedstrunsum).ne.0) then
	        exec write_table_to_file#write dstout [run_dst]
              endif
              exec write_table_to_file#close dstout;
            endif
      (*) message " !!! bfc_util#bfc_finish No final stage for package " [mod]
    ENDCASE

    sigma totime = totime + t[mod]
    if [nevents]>0 then
      sigma cpupev = t[mod]/[nevents]
      string = '---<bfc>---'//[mod]//': CPU/event = '//$format(cpupev,f12.5)
     fmessage $quote([string])    [log_file]
      message $quote([string]) 
    endif

   if (STAF_STATUS(1).ne. STAFCV_OK) goto ABEND
  enddo


  string = '---<bfc>--- total CPU to process  '//$format([nevents],i6)//'events = '//$format(totime,f12.5)
 fmessage $quote([string])    [log_file]
  message $quote([string]) 
  if [nevents]>0 then
      sigma cpupev = totime/[nevents]
      string = '---<bfc>--- total CPU/event = '//$format(cpupev,f12.5)
     fmessage $quote([string])    [log_file]
      message $quote([string]) 
  endif

ABEND:

return
 
**********************************************************************
**********************************************************************
**********************************************************************
 
 macro bfc_print chain='evout'
   
  macro/global/import *
  test = [chain]
  nw = $words([test])
  do i = 1, [nw]
    mod = $word([test],[i],1)
   string = '---<bfc>---'//[mod]//': Event  '//$format([ievent],i6)//$format(v[mod],f12.5)//$format(t[mod],f12.5)
   fmessage $quote([string])    [log_file]
    message $quote([string]) 
  enddo
  
 return
 
*********************************************************************
**********************************************************************
************************************************************
 
macro bfc_g2t_b4_load chain
   
   macro/global/import *
           
 test1 = [chain]
 nw1=$words([test1])
 message ' !!! bfc_g2t_b4_load ==> Number of words in chain = ',[nw1]
 do i=1,[nw1]
   mod1 = $word([test1],[i],1)
   CASE [mod1] in
     (rg2t)
         message ' !!! bfc_g2t_b4_load ==> set params for chain:' [mod1]
* initialize for g2t: tell g2t where to put tables
         detp g2tmain gttc.rdir=0		  
   ENDCASE
 enddo
       
return

**********************************************************************
**********************************************************************
***********************************************************************
******************************************************************
 
 macro bfc_g2t_after_load chain
   
   macro/global/import *
     
*  must fill Run tables if running g2t (rg2t in chain)
*  has to be done after loading
 test2 = [chain]
 nw2=$words([test2])
 message ' !!! bfc_g2t_after_load ==> Number of words in chain = ',[nw2]
 do i=1,[nw2]
   mod2 = $word([test2],[i],1)
   CASE [mod2] in
     (rg2t) 
       message ' !!! bfc_g2t_after_load ==> setup for chain:' [mod2]
* this command fills geant Run tables
        tables ' ' [geant_Run]
   ENDCASE
 enddo
  
return



**********************************************************************
**********************************************************************
**********************************************************************

















