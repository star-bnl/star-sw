* *************************************************************************
* run_ftpc_slow_sim_tracker.kumac
* 
* 
* created July 28,1998 - Janet Seyboth
*                        (with lots of help from Kathy Turner)
*                      - this is now the kumac that drives the ftpc slow 
*                        chain: simulation,tracking, track evaluation
* editted July 29,1998 - K. Turner
*                         changed some names of utility kumacs
*                         write out [run] and [event]
* 8/10/98 - K. Turner - added nid to call to write_table_to_ntuple#close_disk 
************************************************************************
************************************************************************

 macro run__
          TOP=$STAR _
          domain='ftpc' _
          input_data_prefix=$AFS_RHIC/star/users/jcs/public/ _
          input_data_file=auau_gstar_g2t.tab _
          no_events=1 _
          funit=21 _
          nfile=ftpc_tracks.ntup _
          nid=111 _
          table=ftpc/tracks/fpt_fptrack _
          output_file=ftpc_slow_sim_output.xdf
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_ftpc_slow_sim_tracker
* 

***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]

 ***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** TOP level directory     --> ' [TOP]
 message ' *** Input file              --> ' [input_file]        
 message ' *** Num Events to process   --> ' [no_events]
 message ' *** domains to load         --> ' [domain]
 
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]
  message ' *** Output file name        --> ' [output_file]
  
  
***********************************************************
* GLOBAL VARIABLES 
***********************************************************
    
 macro/global/create input_file  [input_file]   'Input file name'
 macro/global/create TOP              [TOP]

   
***********************************************************
* BEGINNING STUFF 
***********************************************************
   
 exec $STAR/kumacs/util/setup_defaults 
   
 exec setup#setup 
 
 macro/global/import *
   
 exec load_libraries#run        [domain]   
   
* **************************************************************
* OPEN FILES
* ***************************************************************
 
* open input g2t data file
  exec input_g2t_file#init [input_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  

* open output file
  exec write_table_to_file#init evout [output_file]
   
* **************************************************************
* --------- Setup and run ftpc slow simulator & tracker pams -------------*
* ****************************************************************
  

* create tables
    
    exec create_tables_ftpc#fss_tables
    exec create_tables_ftpc#fcl_tables
    exec create_tables_ftpc#fpt_tables
    exec create_tables_ftpc#fte_tables

* initialize tables
    
    exec fss_init#init
    exec fcl_init#init 
    exec fpt_init#init
    exec fte_init#init

* write run directory to output table file
   exec write_table_to_file#write evout [run]

  
* ------------------- process events ------------------------------*

* read event and then process thru tpc slow sim & then tracker
  do i=1,[no_events]
    message ' run_ftpc_slow_sim_tracker: processing event ' [i]
* clear all event tables
    tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
* ftpc slow simulator 
    exec fss_run#run
      if STAF_STATUS(1).eq.0 then
        message ' problem with fss '
      endif
* ftpc cluster finder
      exec fcl_run#run
        if STAF_STATUS(1).eq.0 then
          message ' problem with fcl '
        endif
* ftpc tracking
	exec fpt_run#run_slow_sim
	if STAF_STATUS(1).eq.0 then
	  message ' problem with fpt '
	endif  
* ftpc track evaluation
	exec fte_run#run_slow_sim
	if STAF_STATUS(1).eq.0 then
	  message ' problem with fte '
	 endif 
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
* write event directory to output table file
   exec write_table_to_file#write evout [event]
  enddo

  
* -------------------- end stuff -----------------------------*
  
  ENDOFDATA:
    if [i].lt.[no_events] then
    message ' End of input file'
  endif  

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit] [nid]

* close input file
  message ' close input file'
  exec input_g2t_file#close

* close output table file
  exec write_table_to_file#close evout

return
 
************************************************************************

