* *************************************************************************
* run_ctb.kumac
* 
* 7/28/98 K. Turner - editted to make it standard
* 7/29/98 K. Turner - editted to use new utility functions
* 8/10/98  K. Turner - added nid to call to write_table_to_ntuple#close_disk
* 10/14/98 K. Turner - put in read_default_params
* 11/16/98 K. Turner - update to run in with current ctf kumac
* ***********************************************************************
************************************************************************

 macro run__
          TOP=$STAR _
          domain='ctf' _
          INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
          input_g2t_file=muons_central_2.xdf _
          num_events=1 _
          writegeom=0 _
          funit=21 _
          nfile=ctb.ntup _
          nid=111 _
          table=ctf/ctb_cor
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_ctb
* 
	  
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** Data Sample directory -->              ' [INPUT_DATA_DIR]
  message ' *** Input G2T file -->                     ' [input_g2t_file]
  message ' *** Num Events to process -->              ' [num_events]
  message ' *** write out geometry to file yes,no = 1,0 ' [writegeom]
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]

***********************************************************
* General Setup
***********************************************************
  
 macro/global/create TOP              [TOP]

   exec $STAR/kumacs/util/setup_defaults#run
   
   exec setup#setup
   
   macro/global/import *
     
   exec load_libraries#run [domain]
   exec read_default_params#init
* **************************************************************
* --------- Setup and run tpc fast simulator & tracker pams -------------*
   
* open input g2t data file
  exec input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  
   
* --------------------- initialization ----------------------------*
  
  
  exec create_tables_ctf#init
  exec ctg_init#init
  exec ctg_init#ctg
  exec cts_init#init

  
* ------------------- process events ------------------------------*

* read event and then process thru simulator
  do i=1,[num_events]
    message ' run_ctb: processing event ' [i]
* clear all event tables
    tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
* do simulation
    exec cts_run
* fill pixel tables
    exec ctu_run
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
  enddo

  
* -------------------- end stuff -----------------------------*
  
  ENDOFDATA:
  if [i].lt.[num_events] then
    message ' End of input file'
  endif  

* close ntuple
  exec write_table_to_ntuple#close_disk_ntup
  exec write_table_to_ntuple#close_file_ntup [funit]
  exec write_table_to_ntuple#delete_soc_ntup [nid]

* close input file
  message ' close input file'
  exec input_g2t_file#close

return
 
************************************************************************













