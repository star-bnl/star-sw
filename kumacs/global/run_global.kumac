* *************************************************************************
* run_global.kumac
* needs file with all tpc and svt information on it as input
*
* 08/11/98 K. Turner       - created
* 08/12/98 K. Turner       - added dst_monitor_soft_filler
* 08/27/98 D. Weerasundara - added  init_dst#init_rhead
*                            added  init_dst#init_ehead
*                            added  run_dst#dst_event_summary
*
************************************************************************
************************************************************************

 macro run__
          TOP=$STAR _
          domain='tpc ctf svt global' _
          input_data_prefix=/net/rmds03/disk1/star/kathy/ _
          input_data_file=auau_ce_b0-2_4041_4060_tpc_svt_tracks.xdf _
          no_events=1 _
          funit=21 _
          nfile=global.ntup _
          nid=111 _
          table=globtrk
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_global
* 
* ***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]

***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
  
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** Data Sample directory -->              ' [input_data_dir]
  message ' *** Input G2T file -->                     ' [input_data_file]
  message ' *** Num Events to process -->              ' [no_events]

  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]
  
***********************************************************
* BEGINNING STUFF 
***********************************************************
  
 macro/global/create TOP              [TOP]

   exec $STAR/kumacs/util/setup_defaults#run
   
   exec setup#setup
   
   macro/global/import *
     
   exec load_libraries#run [domain]

**************************************************************
* OPEN FILES
****************************************************************
 
* open input data file
  exec read_table_from_file#init simd [input_file]
  
* read run tables from input file
  exec read_table_from_file#read simd
  
  if STAF_STATUS(1).eq.stafcv_bad goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  
  

* **************************************************************
*  Setup for global
* ****************************************************************  
  
  mkdir [params]/global/magnetic_field

* evr - must be before egr
  exec init_evr#init
   
* svm
  exec create_tables_global#svm
  exec init_svm#init

* egr
  exec create_tables_global#egr
  exec init_egr#init
  
* ev0
  exec create_tables_global#ev0 
  exec init_ev0#init
  exec init_magf#init
   
* dst_dedx_filler - must be after tpc tracking
  exec create_tables_global#dst_dedx_filler

* dst_monitor_soft_filler - call at end!
*           needs info from svt, tpc, ctf, global 
* just create ctf tables for now since don't have them in input file!
  exec create_tables_ctf#init
  exec create_tables_global#dst_monitor_soft_filler

* dst_point_filler
 exec create_tables_global#dst_point_filler

* fill_dst_event_summary
  exec create_tables_global#fill_dst_event_summary

* dst_event_header
   exec create_tables_global#fill_dst_event_header

* fill_dst_run_summary
   exec create_tables_global#fill_dst_run_summary

* dst_run_header
   exec create_tables_global#fill_dst_run_header
   exec init_dst#init_rhead

***************************************************************
*  Run global
* ****************************************************************

* Initialize event counters.   DSW  Aug 28, 1998
  ntotevent=0
  ngoodevents=0

* read event and then process thru simulator
  do i=1,[no_events]
    message ' run_global: processing event ' [i]
* clear all event tables
    tabclear [event]
* read input file
    exec read_table_from_file#read simd
    if STAF_STATUS(1).eq.stafcv_bad goto ENDOFDATA
* count the total number of events processed .    DSW  Aug 28, 1998
   ntotevent=[ntotevent]+1
* fill event_header table - must be done in event loop or it
* gets wiped out!!!
    exec init_dst#init_ehead
* svm
     exec run_svm#run_svm
* evr  - must be before egr
   exec run_evr#run
* egr
     exec run_egr#egr
* ev0
     exec run_ev0#ev0_am
* dst_dedx_filler - must be after tpc_tracking
     exec run_dst#dst_dedx_filler
* dst_point_filler
     exec run_dst#point_filler
* dst_event_summary - must be called before dst_monitor_software 
     exec  run_dst#dst_event_summary
* dst_monitor_software - call this last!
     exec run_dst#dst_mon_soft
* dst_run_summary - must be called in every event to update sum variables.
* DSW  Aug 28, 1998
    exec  run_dst#dst_run_summary
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [dst]/[table]
* Count the total #of events processed successfully.  DSW  Aug 28, 1998
    if STAF_STATUS(1).eq.stafcv_ok  then
      ngoodevents=[ngoodevents]+1
    endif
  enddo

  
* -------------------- end stuff -----------------------------*
  
  ENDOFDATA:

* dst_run_summary - final call to fill_dst_run_summary module to fill 
*                   dst_run_summary table. Make sure to fill 
*                   dst_run_summary[0].n_events_good
*                   before making the call fill_dst_run_summary.
*                   DSW  Aug 28, 1998
  cd  [run_dst]
  tdm/table/cell/putvalue  'run_summary[0].n_events_tot'   [ntotevent]
  tdm/table/cell/putvalue  'run_summary[0].n_events_good'  [ngoodevents]
  exec  run_dst#dst_run_summary

* Fill run_summary[0].cpu_tot for this run here.  DSW  Aug 28, 1998

  cd /dui

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit] [nid]
  

* close input file
  message ' close input file'
  exec read_table_from_file#close simd

return
 
************************************************************************













