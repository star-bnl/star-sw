* ********************************************************************
* run_gstar_g2t.kumac
* 
* This kumac reads from a file and runs gstar then g2t.
* 
******************************************************************
* 
* created 6/5/98 Kathy Turner
* editted 6/8/98 Kathy Turner - put tables in correct directories
*                             - and make so can enter values by hand
* editted 6/9/98 Kathy Turner - don't need to move directories anymore
* editted 6/26/98 Kathy Turner - now open input file using "dummy" - now
*                                you must make sure that you have correct
*                                extension on your file! (see note below)
* editted 7/8/98 Kathy Turner - put in default STAR geometry
*                               and make kumac more standard
* editted 7/10/98 Kathy Turner - change directories where tables get written
* editted 7/21/98 Kathy Turner - changed names of kumacs to make them
*                                   more standard 
* editted 7/29/98 K. Turner - use more of standard utility kumacs
* editted 8/3/98  K. Turner - use detp to setup geometry
* editted 8/11/98 K. Turner - make setting split_off hardwired in for gstar
* editted 8/12/98 K. Turner - combine geomtype+phystype into gstar_settings
* 
* *******************************************************************
* ---------available values of input parameters ---------------
* 
* gstar_settings = hadr_on, hadr_off, decay_only
*    tpc_only, field_only, complete, 4th_off (for svt 4th layer off)
* *** there are many other commands setup in geometry.g
*     which you can find out about by doing 'detp geometry help' in staf and
*     then seeing the list when geometry libraries are loaded
* 
* intype = file,phase
*    The input file can be from an event generator or a txt file 
*    OR: can enter values by hand and use "phase" - use intype=phase
* 
* *****************************************************************
* ***** IMPORTANT NOTE *************
* - See also notes from Pavel Nevski on his WWW page about this:
* - The extension of the input file (first letter) tells Geant
*   what type of file it is and can therefore read it correctly.
*    .t is text file
*    .x is xdf file - if you are reading directly from xdf file use this
*    .s is staf tables  - if you are using commands to write a staf table
*                   to use for geant OR to read from a staf table file,
*                   then use this extension.
* 
* ******************************************************************
* input_data_dir=$AFS_RHIC/star/data/samples/ _
* input_file=muons_central_2.txt _
* output_file=muons_central.xdf
* 
* input_data_dir=/star/mds/data/SD98/auau200/evg/central/hijing/set0001/regular/ _
* input_file=auau_ce_b0-2_1_200.xdf _
* output_file=auau_central_hijing.xdf
* 
 macro run__
   TOP=$STAR _
   domain='geometry sim/gstar sim/g2t' _
   chain='rgst rg2t' _
   gstar_settings='complete 4th_off hadr_on' _
   num_events=1 _
   debugger=on _
   intype=file _
   input_data_dir=$AFS_RHIC/star/data/samples/ _
   input_file=muons_central_2.txt _
   output_file=muons.xdf               
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_gstar_g2t
* 
   
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** chain to use -->                       ' [chain]
  message ' *** gstar settings -->                     ' [gstar_settings]
  message ' *** Num Events to process -->              ' [num_events]
  message ' *** debugger on/off -->                    ' [debugger]
  message ' *** input type: phase, file -->            ' [intype]
  message ' *** Input Data directory -->               ' [INPUT_DATA_DIR]
  message ' *** Input file -->                         ' [input_file]
  message ' *** output file -->                        ' [output_file]

* ----------- General setup stuff ------------------------*
  macro/global/create TOP           [TOP]
  macro/global/create gstar_settings [gstar_settings]
          
* setup default directories to search for kumacs in
    exec $STAR/kumacs/util/setup_defaults 
          
* setup default table directory structure in STAF
     exec setup#setup
     
*  must have this after setup!
   macro/global/import *
       
* THESE NEXT 4 MUST BE DONE IN THIS ORDER!!!!
     message ' *** SET GEOMETRY in gstar  -->   ' [gstar_settings]
     message '      !!! split_off is hardwired in !!! '
   detp geometry [gstar_settings] split_off
   exec bfc_util#bfc_g2t_b4_load    [chain]
   exec load_libraries#run          [domain]
   exec bfc_util#bfc_g2t_after_load [chain]

******** turn debugger on just for GSTAR! ******************************
 if [debugger].eq.on then
   debug on
 endif
   
****************** OPEN OUTPUT FILE *******************
 exec write_table_to_file#init outunit [output_file]
 
*********** WRITE RUN INFO TO OUTPUT FILE ***************
* write Run information to output file - already in staf format 
 exec write_table_to_file#write outunit [geant_Run] 
 
************* FOR DEBUGGING *********************************
*  just for looking at output of gstar
 if [debugger].eq.on then
  swit 2 3
  next; dcut cave z 225 10 10 .04 .04
 endif
 

* ************ OPEN INPUT FILE *********************************
* open input file - this in in GEANT format!
 
 if [intype].eq.file then
   user/input dummy [input_data_dir][input_file]
   message ' use input file to run '
 elseif [intype].eq.phase then
   message ' enter particles to generate by hand '
 else
   message 'invalid input type'
 endif
 
 
*------------------- process events ------------------------------*
  
 do i = 1,[num_events]
   message ' run_gstar: processing event  '  [i]  
* clear all event tables
  tabclear [event]
* if inputting values by hand:
     if [intype].eq.phase then
       exec gstar_input_phase#init
      endif
*  do for debugging:
     if [debugger].eq.on then 
       next; dcut cave z 225 10 10 .04 .04
     endif
* trig 1 - automatically reads next event and runs gstar
* g2t - go to geantE directory, then transform geant structures
*       to staf tables which will be put in event/geant/Event
     trig 1
     ls [geantE]
     cd [geantE]
     for/call g2t
     cd /dui
* write out to output_file
 exec write_table_to_file#write outunit [geant_Event]
 enddo
   
* -------------------- end stuff -----------------------------*
   
   exec write_table_to_file#close outunit
* 
   
  return
* ***************************************************************















