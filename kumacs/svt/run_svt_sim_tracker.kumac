* SYNOPSIS: STAF analysis driver for the SVTanalysis
*
* DESCRIPTION:
*    A kumac STAF driver for SVT data
*    analysis. The kumac includes user setable switches to let
*    the user decide what packages are to be used for specific
*    tasks.
* Authors: H. Caines
*
* History:
*          MARCH 18, 98 - created
* June 3, 1998 - editted by Kathy Turner
* 7/11/98 - K. Turner - change default data file to new version
* 7/11/98 - K. Turner - change to use standard setup kumacs
* 7/14/98 - K. Turner - change to use bfc_defaults instead of bfc_star
* 7/30/98 - K. Turner - change to use new utility kumacs
* 8/10/98 - K. Turner - added nid to call to write_table_to_ntuple#close_disk
* 8/10/98 - K. Turner - fixed up init calls
* 8/27/98 - K. Turner - fixed up init calls
* *************************************************
 
 MACRO run _
        TOP=$STAR _
        domain=' svt ' _
        input_data_prefix=$AFS_RHIC/star/data/samples/ _
        input_data_file=auau_central_hijing.xdf _   
        no_events=1 _   
	svt_simul=srs _
	svt_tracker=sgr _  
        funit=21 _
        nfile=svt_tracks.ntup _
        nid=111 _
        table=svt/hits/scs_spt
   
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_svt_sim_tracker
* 
***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]
   
 ***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** TOP level directory     --> ' [TOP]
 message ' *** Input file              --> ' [input_file]        
 message ' *** Num Events to process   --> ' [no_events]
 message ' *** domains to load         --> ' [domain]
 
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]

  message ' *** svt simulator -->                      ' [svt_simul]
  message ' *** svt tracker  --->                      ' [svt_tracker]
  
  
*  have to have these or svt kumacs won't work!!!
  alias/create STAFCV_OK  1
  alias/create STAFCV_BAD 0 
  
  
***********************************************************
* GLOBAL VARIABLES 
***********************************************************
    
 macro/global/create input_file  [input_file]   'Input file name'
 macro/global/create TOP              [TOP]
   
***********************************************************
* BEGINNING STUFF 
***********************************************************
   
 exec $STAR/kumacs/util/setup_defaults 
   
 exec setup#setup 
 
 macro/global/import *
      
 exec load_libraries#run  [domain]  
   
**************************************************************
* OPEN FILES
****************************************************************
 
* open input g2t data file
  exec input_g2t_file#init [input_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile] 
  
  
* **************************************************************
*  Setup for svt fast sim & tracking
* ****************************************************************
  
  exec  init_svg#read
  exec  init_svg#svg_am
  
 if [svt_simul] = sss then
    exec init_sss#init
    exec init_ssf#init
 elseif [svt_simul] = srs then
   exec create_tables_svt#srs
   exec init_srs#init
 endif
 
 
 if [svt_tracker].eq.stk .or. [svt_tracker].eq.sgr then
   exec create_tables_svt#sgr
   exec create_tables_svt#stk
   exec init_sgr#init
   exec init_stk#init
   exec init_vertices#init
 endif
 
 
* **************************************************************
*  Run svt fast sim & tracking
* ****************************************************************

 do ievent=1,[no_events]
    message ' run_svt_sim_tracker: processing event ' [ievent]
* clear all event tables
  tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
   message 'cpu time is ' $cptime
* SVT detector response simulation
    if [svt_simul]= sss then
      exec run_sss
      exec run_ssf
      if (staf_status(1) .ne. 1) goto NEXT_EVENT
    elseif [svt_simul]= srs then
       exec run_srs  
       if (staf_status(1) .ne. 1) goto NEXT_EVENT
    else
      message ' SVT simulator not valid!!'
    endif
* SVT track finding and momentum reconstruction -----------------------
    if [svt_tracker]= stk then
      exec run_stk#run
        if (staf_status(1) .ne. 1) goto NEXT_EVENT
    elseif [svt_tracker]=sgr then
      exec run_sgr#run
      exec run_stk#run
        if (staf_status(1) .ne. 1) goto NEXT_EVENT
    else
      message ' svt_tracker not valid! '
    endif
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]    
    
  NEXT_EVENT:
   
 enddo
 
* -------------------- end stuff -----------------------------*
    
ENDOFDATA:


* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit] [nid]

* close input file
  message ' close input file'
  exec input_g2t_file#close

return
 

 
* **************************************************************
