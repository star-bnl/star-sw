************************************************************************
MACRO TOP_JOIN [OPT]=-h [NROWS]=12
EXEC INIT
CASE [OPT] IN
(-h,-H)
	EXEC HELP
(0)
	EXEC TOP_JOIN1 [NROWS]
(1)
	EXEC TOP_JOIN1 [NROWS]
(*)
	EXEC HELP
ENDCASE
RETURN
************************************************************************
MACRO HELP
MESS ------------------------------------------------------------------
MESS TOP_JOIN.kumac
MESS
MESS Template file for test macros including standard macro targets.
MESS
MESS USAGE: EXEC TOP_JOIN <OPT> <NROWS>
MESS
MESS .      OPT=-h          Show this help message.
MESS .      OPT=1           Test TOP/JOIN/ commands.
MESS .      OPT=0           Run ALL tests.
MESS .      NROWS           Number of rows of tables to create.
MESS
MESS ------------------------------------------------------------------
RETURN
************************************************************************
MACRO INIT
CD; MKDIR top_join; CD top_join !** Only works with DUI loaded.
RETURN
************************************************************************
MACRO TOP_JOIN1 [NROWS]=12

MKDIR test_join
CD test_join

MESS
MESS **************************************************
MESS ***** Create tables for join                  ****
MESS **************************************************

NEWTABLE q 'struct fourvect{ long id; float x,y,z; }' 100
NEWTABLE p 'struct fourvect{ long id; float p[3],e; }' 100
TABLE/ROWCOUNT q [NROWS]
TABLE/ROWCOUNT p [NROWS]
DO I=0,[NROWS]-1
	CELL/PUTVALUE 'q['//[I]//'].id' [i]
	CELL/PUTVALUE 'q['//[I]//'].x' [i]
	CELL/PUTVALUE 'q['//[I]//'].y' [i]
	CELL/PUTVALUE 'q['//[I]//'].z' [i]
	j = 2*[i]
	CELL/PUTVALUE 'p['//[I]//'].id' [j]
	CELL/PUTVALUE 'p['//[I]//'].p' [i] 1+[i] 2+[i]
	CELL/PUTVALUE 'p['//[I]//'].e' [i]
ENDDO

MESS
MESS **************************************************
MESS ***** Create two joins 1 has no select clause ****
MESS *****   the other has a select clause         ****
MESS **************************************************
NEWJOIN jn ! '{id}'
NEWJOIN jn2 '{q.x x2, q.y y2, p.p g}' '{id}'

MESS
MESS **************************************************
MESS *** selectspec does not work if defined       ****
MESS *** with a ! get a seg. violation             ****
MESS **************************************************

*TOP/JOIN_AGENT/SELECTSPEC jn
TOP/JOIN_AGENT/SELECTSPEC jn2
TOP/JOIN_AGENT/WHERECLAUSE jn
TOP/JOIN_AGENT/WHERECLAUSE jn2

WAIT ' Hit <cr> to continue:' 0

MESS
MESS **************************************************
MESS ***** Create two joins 1 has no select clause ****
MESS *****   the other has a select clause         ****
MESS **************************************************
JOIN jn q p x
JOIN jn2 q p x2

TABLE/PRINT q
TABLE/PRINT p
TABLE/PRINT x
TABLE/PRINT x2
LS

WAIT ' Hit <cr> to continue:' 0


MESS
MESS **************************************************
MESS ***** Now use  fast join.                     ****
MESS ***** First get data and sort tables in track_p ****
MESS **************************************************


dio/newf fjoin $STAR_DATA/samples/test_data.xdf R
dio/stream/getevent fjoin
dio/stream/getevent fjoin
cd Event

TOP/NEWSORT xsort track_p
TOP/SORT_AGENT/COLUMN xsort

do i=0,20
tdm/table/cell/getvalue 'g2t_svt_hit['//[i]//'].track_p'
enddo

TOP/SORT_AGENT/SORT xsort g2t_svt_hit
do i=0,20
tdm/table/cell/getvalue 'g2t_svt_hit['//[i]//'].track_p'
enddo

TOP/NEWSORT tsort id
TOP/SORT_AGENT/SORT tsort g2t_track
do i=0,20
tdm/table/cell/getvalue 'g2t_track['//[i]//'].id'
enddo

WAIT 'Hit <cr> to continue:' 0


MESS
MESS **************************************************
MESS ***** Now use  fast join.                     ****
MESS **************************************************

TOP/NEWJOIN ff '{g2t_svt_hit.x svt_x, g2t_svt_hit.track_p track_p, g2t_track.p t_p}' '{track_p g2t_track.id}'
TOP/JOIN_AGENT/FASTJOIN ff g2t_svt_hit g2t_track track_hits 
ls
table/print track_hits 30 1
WAIT 'Hit <cr> to continue:' 0

MESS
MESS **************************************************
MESS ***** End of join testing                     ****
MESS **************************************************

RETURN
************************************************************************
