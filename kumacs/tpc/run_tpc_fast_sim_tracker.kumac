* *************************************************************************
* run_tpc_fast_sim_tracker.kumac
* 
* 
* created May 27, 1998 - Kathy Turner
*                      - this is now kumac that drives tpc fast simulator 
*                        and then does tracking
* editted June 4, 1998 Kathy Turner
* editted 6/25/98 Lidia Didenko
* editted 6/25/98 Kathy Turner
* 7/2/98 - K. Turner - code now requires that you load geometry libraries
* 7/3/98 - K. Turner - make to use bfc_util kumacs and include pam kumacs
* 7/11/98 - K. Turner - change default data file to new version
* 7/13/98 - K. Turner - change to use bfc macros
* 7/30/98 - K. Turner - change to use new utility kumacs
* 8/10/98 - K. Turner - added nid to call to write_table_to_ntuple#close_disk
* 8/10/98 - K. Turner - fixed for change to tpg_init
* 8/26/98 - K. Turner - fix for changes to init kumacs
* 10/14/98 K. Turner - put in read_default_params
* 
************************************************************************
************************************************************************

 macro run__
          TOP=$STAR _
          domain='geometry tpc' _
          input_data_prefix=$AFS_RHIC/star/data/samples/ _
          input_data_file=auau_central_hijing.xdf _
          no_events=1 _
          tracker=tpt _
          funit=21 _
          nfile=tpc_tracks.ntup _
          nid=111 _
          table=tpc/tracks/tptrack
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_tpc_fast_sim_tracker
* 
	***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]

 ***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** TOP level directory     --> ' [TOP]
 message ' *** Input file              --> ' [input_file]        
 message ' *** Num Events to process   --> ' [no_events]
 message ' *** domains to load         --> ' [domain]
 
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]

  message ' *** tpc tracker  --->                      ' [tracker] 

***********************************************************
* GLOBAL VARIABLES 
***********************************************************
    
 macro/global/create input_file  [input_file]   'Input file name'
 macro/global/create TOP              [TOP]

***********************************************************
* BEGINNING STUFF 
***********************************************************
   
 exec $STAR/kumacs/util/setup_defaults 
   
 exec setup#setup 
 
 macro/global/import *
   
 exec load_libraries#run  [domain]  
 
 exec read_default_params#init
 
**************************************************************
* OPEN FILES
****************************************************************
 
* open input g2t data file
  exec input_g2t_file#init [input_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  
  
* **************************************************************
*  Setup for tpc fast sim & tracking
* ****************************************************************
  

* tpc geometry init
  exec tpg_init#read
  exec tpg_init#tpg_main
  
* tpc cluster init for fast sim tracking!
    exec create_tables_tpc#tcl_fast
    exec tcl_init#read 
    
* tpc fast sim init
    exec tfs_init#read
    
* tpc tracker init
    exec create_tables_tpc#tpt
    exec tpt_init#read
    exec tid_init#read
   
* tpc evaluation init
    exec create_tables_tpc#tte
    exec tte_init#read

* **************************************************************
*  Run tpc fast sim & tracking
* ****************************************************************

* read event and then process thru tpc fast sim & then tracker
  do i=1,[no_events]
    message ' run_tpc_fast_sim_tracker: processing event ' [i]
* clear all event tables
    tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
* tpc fast simulator 
    exec tfs_run#run
      if STAF_STATUS(1).eq.0 then
        message ' problem with tfs '
      endif
* tpc tracking
    if [tracker]=tpt then
	exec tpt_run#run
	exec tid_run#run_tde_new      
    elseif [tracker]=tte then
	exec tte_run#run_tte_track
	exec tid_run#run_tde_new
    endif
* tpc track evaluation
    exec tte_run#run_tte
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
  enddo

  
* -------------------- end stuff -----------------------------*
  
  ENDOFDATA:
    if [i].lt.[no_events] then
    message ' End of input file'
  endif  

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit] [nid]

* close input file
  message ' close input file'
  exec input_g2t_file#close

return
 
************************************************************************













