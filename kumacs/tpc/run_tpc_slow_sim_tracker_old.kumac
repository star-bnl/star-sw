* *************************************************************************
* run_tpc_slow_sim_tracker_old.kumac
* 
* created May 27, 1998 - Kathy Turner - runs tpc slow simulator & does tracking
* editted June 4, 1998 Kathy Turner
* editted 6/9/98 Kathy Turner
* editted 6/25/98 Lidia Didenko
* editted 6/25/98 Kathy Turner
* 7/2/98 - K. Turner - code now requires that you load geometry libraries 
* 7/3/98 - K. Turner - make to use bfc_util kumacs and include pam kumacs
* 7/11/98 - K. Turner - change default data file to new version
* 7/14/98 - K. Turner - added global macros
* 7/30/98 - K. Turner - change to use new utility kumacs
* 8/10/98 - K. Turner - added nid to call to write_table_to_ntuple#close_disk
* 8/10/98 - K. Turner - fixed for change to tpg_init
* 8/26/98 - K. Turner - fix for change to init kumacs
* 10/14/98 K. Turner - put in read_default_params 
* 
************************************************************************
************************************************************************

 macro run__
          TOP=$STAR _
          domain='geometry tpc' _
          input_data_prefix=$AFS_RHIC/star/data/samples/ _
          input_data_file=auau_central_hijing.xdf _
          no_events=1 _
          tracker=tpt _
          funit=21 _
          nfile=tpc_tracks.ntup _
          nid=111 _
          table=tpc/tracks/mctrk_
          tpc_sector_first=1_
          tpc_sector_last=1_
          adcxyzon=0
   
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_tpc_slow_sim_tracker_old
* 
* ***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]

 ***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** TOP level directory     --> ' [TOP]
 message ' *** Input file              --> ' [input_file]        
 message ' *** Num Events to process   --> ' [no_events]
 message ' *** domains to load         --> ' [domain]
 
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]

  message ' *** tpc tracker  --->                      ' [tracker] 

***********************************************************
* GLOBAL VARIABLES 
***********************************************************
    
 macro/global/create input_file  [input_file]   'Input file name'
 macro/global/create TOP              [TOP]

***********************************************************
* BEGINNING STUFF 
***********************************************************
   
 exec $STAR/kumacs/util/setup_defaults 
   
 exec setup#setup 
 
 macro/global/import *
   
 exec load_libraries#run  [domain]  
 
 exec read_default_params#init
 
**************************************************************
* OPEN FILES
****************************************************************
 
* open input g2t data file
  exec input_g2t_file#init [input_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  
  
* **************************************************************
*  Setup for tpc slow sim & tracking
* ****************************************************************
  
* tpc geometry init
  exec tpg_init#read
  exec tpg_init#tpg_main
  
*  tpc slow sim init
    exec create_tables_tpc#tss_old
    if [adcxyzon].eq.1 then
      exec create_tables_tpc#tfc
    endif
    exec tss_init#read
    exec tss_init#init [tpc_sector_first] [tpc_sector_last] 

* tpc cluster init
   exec create_tables_tpc#tcl_slow_old
   exec tcl_init#read 
     
* tpc tracker init
   exec create_tables_tpc#tpt
   exec tpt_init#read
   exec tid_init#read
   
* tpc evaluation init
   exec create_tables_tpc#tte
   exec tte_init#read 
   
* **************************************************************
*  Run tpc slow sim & tracking
* ****************************************************************

  
* read event and then process thru tpc slow sim
  do i=1,[no_events]
    message 'run_tpc_slow_sim_tracker: processing event ' [i]
* zero all tables
    tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
* slow simulator
    exec tss_run#run_old
* unpack raw data for diagnostics
    if [adcxyzon].eq.1 then
      exec tfc_run#run
    endif
* clustering
    exec tcl_run#run_tcl_make_clusters
    exec tcl_run#run_tpham
*  tracking
    if [tracker]=tpt then
      exec tpt_run#run
      exec tid_run#run_tde_new      
    elseif [tracker]=tte then
      exec tte_run#run_tte_track
      exec tid_run#run_tde_new
    endif

* tpc track evaluation
    exec tte_run#run_tte

* write table to ntuple 
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
  enddo
  
  
* -------------------- end stuff -----------------------------*
  
  ENDOFDATA:
    if [i].lt.[no_events] then
    message ' End of input file'
  endif  

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit] [nid]

* close input file
  message ' close input file'
  exec input_g2t_file#close

return
 
************************************************************************













