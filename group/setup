#!/bin/csh -f

# Help
if ($#argv == 0 || $#argv > 3 || "$1" == "help" || "$1" == "-h") then
    /bin/cat <<EOF

 Usage is:
   % setup 'Package' {version}

 where packages stands for one of
   debug|DEBUG                      switch the environment to the debug version of 
                                    the STAR software
   nodebug|NODEBUG                  switch the environment to the nodebug (optimized) 
                                    of the STAR software
   64bits|64b|32bits|32b            Switch to 64 or 64 bits env

   gprof|GRPOF                      enables/disables gprof profiling environment
   nogprof|NOGPROF                  non-optimized version will be implied when enabled


   Grid|OSG                         Setup Grid environment
   geant4        {version}          load Geant4 env
   root|ROOT     {version}          load or change root version
   cern|CERN     {version} {InstallRoot} 
                                    Re-define CERN related ENV variables
				    

   parasoft|PARASOFT|insure|ISURE   load the Insure++ toolkit environment
   gcc           {version}          set environment to use gcc (or alternative version)
   icc|ifc|ifort {version}          set environment to use Intel compilers
   PGI|pgi                          set PGI path for using its compiler

   alt|noalt                        adds/remove OPTSTAR/alt from path

EOF
    exit 0
endif

set pkg = $1; shift;
set version  = 0;
set location = 0;
if ( $#argv > 0 ) set version  = $1
if ( $#argv > 1 ) set location = $2
setenv oldSTAR_HOST_SYS $STAR_HOST_SYS

source $GROUP_DIR/unix_programs.csh

switch ($pkg)
    case "64bits":
    case "64b":
      setenv USE_64BITS 1
      unsetenv OPTSTAR
      unsetenv XOPTSTAR
      if (-x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit $STAR_HOST_SYS /opt/star`
        setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $oldSTAR_HOST_SYS /opt/star ^/usr/lib`
      endif
      setup $xSTARCMPL
      source $GROUP_DIR/STAR_SYS
#      setenv CERN /cern64
      setup cern
#      setenv CERN /afs/rhic.bnl.gov/.asis/x8664_sl5/cern64
#      setenv CERN /afs/rhic.bnl.gov/star/packages/.DEV2/misc/sl53_x8664_gcc451
      unsetenv CERN_ROOT
      unsetenv OPTSTAR
      unsetenv QTDIR
      source $GROUP_DIR/group_env.csh
      setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $oldSTAR_HOST_SYS ^/usr/lib /opt/star`
      setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:/usr/lib64:/usr/lib64/mysql
#      setenv ValeryOptStar /afs/rhic.bnl.gov/star/users/fine/data08/STAR/OPTSTAR/.$STAR_HOST_SYS
#      setenv LD_LIBRARY_PATH $ValeryOptStar/lib:$OPTSTAR/lib:$LD_LIBRARY_PATH
#      setenv PATH $ValeryOptStar/bin:$OPTSTAR/bin:$PATH
#      setenv QTDIR $ValeryOptStar/Qt4
#      if ($?QTDIR) setenv QTDIR $OPTSTAR/qt4
#      setenv PATH $QTDIR/bin:$PATH
#      setenv LD_LIBRARY_PATH $QTDIR/lib:$LD_LIBRARY_PATH
       source ${GROUP_DIR}/.starver $STAR_LEVEL
     breaksw

    case "32bits":
    case "32b":
      setenv USE_64BITS 0
      unsetenv OPTSTAR
      unsetenv XOPTSTAR
      setup $xSTARCMPL
      if (-x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit $STAR_HOST_SYS /opt/star`
        setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $oldSTAR_HOST_SYS /opt/star ^/usr/lib64`
      endif
#      setenv OPTSTAR /afs/rhic.bnl.gov/star/packages/.DEV2/misc/opt/star/$STAR_HOST_SYS
      source $GROUP_DIR/STAR_SYS
#      setenv CERN /cern
      setup cern
      unsetenv CERN_ROOT
      unsetenv OPTSTAR
      unsetenv QTDIR
      source $GROUP_DIR/group_env.csh 
      setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $oldSTAR_HOST_SYS lib64 lib`
#      setenv ValeryOptStar /afs/rhic.bnl.gov/star/users/fine/data08/STAR/OPTSTAR/.$STAR_HOST_SYS
#      setenv LD_LIBRARY_PATH $ValeryOptStar/lib:$OPTSTAR/lib:$LD_LIBRARY_PATH
#      setenv PATH $ValeryOptStar/bin:$OPTSTAR/bin:$PATH
#      setenv QTDIR $ValeryOptStar/Qt4
#      setenv PATH $QTDIR/bin:$PATH
#      setenv LD_LIBRARY_PATH $ValeryOptStar/Qt4/lib:$LD_LIBRARY_PATH
#      if ($?QTDIR) setenv QTDIR $OPTSTAR/qt4
#      setenv PATH $QTDIR/bin:$PATH
#      setenv LD_LIBRARY_PATH $QTDIR/lib:$LD_LIBRARY_PATH
#      setup $xSTARCMPL
#       source ${GROUP_DIR}/.starver $STAR_LEVEL
      breaksw


    case "PGI":
    case "pgi":
      # PGI compiler - this may be used
      if ( ! $?PGI) then
        # should be defined but does not hurt for now to protect
        if ( ! $?USE_64BITS ) then
	   setenv USE_64BITS 0
	endif
	if ( $USE_64BITS == 1 ) then
	   set x="/usr/pgi64 /usr/pgi"
	else
	   set x="/usr/pgi"
	endif

	foreach d ($x)
	    if ( -d $d ) then
		#echo "Setting PGI to $d"
		setenv PGI $d
		break
	    endif
	end
      endif
      if ( $?PGI ) then
	if ( ! -d $PGI/linux86/bin && -e $PGI/linux86 ) then
	    set version=`ls  $PGI/linux86/ | tail -1`
	    setenv PGI_V linux86/$version
	else
	    setenv PGI_V linux86
	endif
	setenv PATH `${GROUP_DIR}/dropit -p $PGI/$PGI_V/bin -p $PATH`
	if ( -d $PGI/$PGI_V/man) then
	    setenv MANPATH `${GROUP_DIR}/dropit -p ${MANPATH} -p ${PGI}/${PGI_V}/man`
	else
	    setenv MANPATH `${GROUP_DIR}/dropit -p ${MANPATH} -p ${PGI}/man`
	    #alias  pgman 'man -M $PGI/man'
	endif
	setenv PGILIB  ${PGI}/${PGI_V}/lib
	setenv LM_LICENSE_FILE $PGI/license.dat
      endif
      breaksw



    case "OSG":
    case "Grid":
	# Support for OSG - Try to locate things from OSG including Worker Node
	if ( ! $?OSG ) then 
	    if ( -e /opt ) then
		set LOSG=`ls -d /opt/* | $GREP OSG | tail -1`
		if ( "$LOSG" != "") setenv OSG $LOSG
	    endif
	endif
	if ( $?OSG ) then
	    if ( -e $OSG/setup.csh ) then
		# there will alos be a java version coming along but
		# it may be defined prior from /usr/java
		source $OSG/setup.csh
		setenv SAVED_PATH `echo $PATH | sed "s/:/ /g"`
	    endif
	else
	    # Unfortunately, the WN package loads the whole blabla with
	    # java, python and even perl all ...
	    if ( $?WNOSG ) then
		if ( -e $WNOSG/setup.csh ) then
		    setenv GSAVED_PATH   $PATH
		    setenv GSAVED_LDPATH $LD_LIBRARY_PATH
		    # trash the path
		    setenv PATH  /bin:/usr/bin
		    # trash LD
		    unsetenv LD_LIBRARY_PATH
		    # load definitions
		    source $WNOSG/setup.csh
		    # redefine path and ld path
		    setenv PATH             ${GSAVED_PATH}:${PATH}
		    setenv LD_LIBRARY_PATH  ${GSAVED_LDPATH}:${LD_LIBRARY_PATH}
	        endif
	    endif
        endif
	breaksw


    # Geant4 - check first
    case "geant4":
       if ( $version != 0 ) then
	    set v =".$version"
       else
	    set v = ""
       endif
       if ( -e ${OPTSTAR}/geant4$v ) then
	    setenv G4PATH ${OPTSTAR}/geant4$v
	    if ( -e $G4PATH/bin/geant4.csh ) then
		setenv G4BIN ${G4PATH}/bin
		(cd $G4PATH/bin/ && source geant4.csh)
	    else
		if ( $?DECHO ) echo "Geant4 setup script was not found"
	    endif
       else
	    if ( $?DECHO ) echo "Geant4 version $version is not installed"
       endif
       breaksw


    # Root setup
    case "root":
    case "ROOT":
	if (-f $GROUP_DIR/rootenv.csh) then
	    if ( -x $GROUP_DIR/dropit) then
		setenv MANPATH         `$GROUP_DIR/dropit -p ${MANPATH} ROOT`
		setenv PATH            `$GROUP_DIR/dropit -p ${PATH} ROOT`
		setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p ${LD_LIBRARY_PATH} ROOT`
	    endif
	    if ($version != 0) setenv ROOT_LEVEL $version
	    source $GROUP_DIR/rootenv.csh
	endif
	breaksw

    # CERN in case of alternate CERN_ROOT
    case "CERN":
    case "cern":
#    echo "CERN in case of alternate CERN_ROOT"
	if ( $location != 0 ) then
	    # use the specified location, no double check
	    setenv CERN $location
        else
	    # try to determine the location according to pre-2010 logic
	    if ( ! $?USE_64BITS ) then
		# this is also defined in STAR_SYS but ensuring
		# a default value is needed for the test below
		setenv USE_64BITS 0
	    endif
	    if ( ! $?CERN ) then
		# Define one value - some of the path below are historical
		if ( $USE_64BITS == 1) then
		    set x="/cern64 /cern ${AFS_RHIC}/asis/@sys/cern64 ${AFS_RHIC}/asis/@sys/cern /usr/local/cern64 /usr/local/cern /common/star/cern64 /common/star/cern /software/fisyak/STAR/cern64"
		else
		    set x="/cern ${AFS_RHIC}/asis/@sys/cern /usr/local/cern /common/star/cern /software/fisyak/STAR/cern"
		endif
            else
		# Either it was set before by the search above or there is an 
		# external definition. In all cases, if it ends by 64, set as-is
		# otherwise append
		set test=`echo $CERN | $GREP 64`
		if ( "$test" != "") then
		    # strip it
		    setenv CERN `echo ${CERN} | sed "s/64//"`
		endif
		if ( $USE_64BITS == 1 ) then
		    set x="${CERN}64 ${CERN}"
                else
		    set x="${CERN}"
		endif
		unset test
	    endif

	    # NOW we have "x" defined as a search path or a unique path
	    # In case of a unique path set by a site, this will redefine to itself
	    # But this odditiy is needed to deal with the 64 bits convention
	    # ${CERN} ${CERN}64
	    foreach d ($x)
		if ( -e $d ) then
		    setenv CERN $d
		    break
		endif
	    end
	    unset x d
	    if ( ! $?CERN ) then
		# this can happen only when the full search path finds nothing
		if ( $?DECHO ) echo "Could not find a valid path for defining CERN, default to /cern"
		setenv CERN /cern
	    endif
        endif
	
	if ( $version != 0 ) then
	    # if specified, use specific version
	    if ( -e $CERN/$version ) then
		setenv CERN_LEVEL $version 
	    endif
	else
	    # define to pro if not defined
	    if ( ! $?CERN_LEVEL ) then
		setenv CERN_LEVEL pro
	    endif
	endif

	# Finally, check of existence of this path and revert to 
	# pro if not found
	if ( ! -e $CERN/$CERN_LEVEL ) then
	    if ( $?DECHO ) echo "$self :: $CERN/$CERN_LEVEL not found, switch to pro"
		setenv CERN_LEVEL pro
		setenv CERN_ROOT "$CERN/$CERN_LEVEL"
	    endif
	    # << ?? if I fix this endif bock, all goes to hell
	    #    there is a missing if/endif sequence somewhere
	    setenv CERN_ROOT $CERN/$CERN_LEVEL
	endif
	breaksw

    # Parasoft/Isure++ software
    case "insure":
    case "INSURE":
    case "parasoft":
    case "PARASOFT":
	if (-f $GROUP_DIR/parasoftenv.csh) source $GROUP_DIR/parasoftenv.csh
	breaksw
	

    # use of the Intel compiler
    case "icc":
    case "ifc":
    case "ifort":
	unsetenv GPROF  # don't know about -qp ; perhaps later
	if ( -f $GROUP_DIR/intelcc.csh  )  source $GROUP_DIR/intelcc.csh $version
	if ( $?optstar != 0)  setenv  OPTSTAR  $optstar
	if ( $?xoptstar != 0 ) setenv XOPTSTAR  $xoptstar
	if ( $?STARCMPL && -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit $STAR_HOST_SYS`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $STAR_HOST_SYS`
	    unsetenv QTDIR
	endif 
	if ( $?DECHO ) echo "setup icc 0: PATH = $PATH"
	if ( $?DECHO ) echo "setup icc 0: LD_LIBRARY_PATH = $LD_LIBRARY_PATH"
	source $GROUP_DIR/intelcc.csh
	if ( $?DECHO ) echo "setup icc 1: PATH = $PATH"
	if ( $?DECHO ) echo "setup icc 1: LD_LIBRARY_PATH = $LD_LIBRARY_PATH"
	setenv STARCMPL "icc";  setenv xSTARCMPL "icc"; source ${GROUP_DIR}/.starver $STAR_LEVEL
	if ( $?DECHO ) echo "setup icc 2: PATH = $PATH"
	if ( $?DECHO ) echo "setup icc 2: LD_LIBRARY_PATH = $LD_LIBRARY_PATH"
	breaksw

    case "gcc*":
	if ( $?DECHO ) echo "===================== case gcc $pkg =============="
      if ( -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit alt $STAR_HOST_SYS gcc4 /opt/rh/devtoolset`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH alt $STAR_HOST_SYS gcc4 /opt/rh/devtoolset`
      endif
      setenv  STARCMPL "gcc"
      setenv xSTARCMPL $pkg; # "gcc";
      if ($?QTDIR)    unsetenv QTDIR
      if ($?OPTSTAR)  unsetenv OPTSTAR
      if ($?XOPTSTAR) unsetenv XOPTSTAR
      if ($?devtools) unsetenv devtools
      if ( $?DECHO ) echo "setup:reset PATH = $PATH"
      if ( $?DECHO ) echo "setup:reset LD_LIBRARY_PATH = $LD_LIBRARY_PATH"
# specific compiler 
      switch ($pkg)
	case "gcc451":
	setenv PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc451/bin:$PATH
	if (! $USE_64BITS) then
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc451/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	else
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc451/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	endif
	breaksw
	case "gcc452":
	if ( -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit alt $STAR_HOST_SYS gcc4`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH alt $STAR_HOST_SYS gcc4`
	endif
	setenv PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc452/bin:$PATH
	if (! $USE_64BITS) then
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc452/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	else
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc452/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	endif
	breaksw
	case "gcc45*":
	setenv PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc45/bin:$PATH
	if (! $USE_64BITS) then
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc45/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	else
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc45/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	endif
	breaksw
	case "gcc471":
	if ( -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit alt $STAR_HOST_SYS gcc4`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH alt $STAR_HOST_SYS gcc4`
	endif
	    setenv PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc471/bin:$PATH
	if (! $USE_64BITS) then
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc471/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	else
	    setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl5/gcc471/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	endif
	breaksw
	case "gcc482":
	setenv devtools /opt/rh/devtoolset-2
	if (-r ${devtools}) then
	  setenv PATH  ${devtools}/root/usr/bin:${PATH}
	  setenv LD_LIBRARY_PATH  ${devtools}/root/usr/lib64:${devtools}/root/usr/lib:${LD_LIBRARY_PATH}
	  setenv INFOPATH  ${devtools}/root/usr/share/info
	  setenv PCP_DIR   ${devtools}/root
          setenv PERL5LIB  ${devtools}/root/usr/lib64/perl5/vendor_perl:${devtools}/root/usr/lib/perl5:${devtools}/root/usr/share/perl5/vendor_perl:${PERL5LIB}
	  setenv PYTHONPATH  ${devtools}/root/usr/lib64/python2.7/site-packages:${devtools}/root/usr/lib/python2.7/site-packages:/opt/rh/devtoolset-3/root/usr/lib64/python2.6/site-packages:/opt/rh/devtoolset-3/root/usr/lib/python2.6/site-packages
	  setenv PKG_CONFIG_PATH  	
	  setenv XDG_CONFIG_DIRS  ${devtools}/root/etc/xdg:/etc/xdg
	  setenv XDG_DATA_DIRS  ${devtools}/root/usr/share:/usr/local/share:/usr/share
          setenv X_SCLS  `basename $devtools`
	endif
	breaksw
	case "gcc492":
	setenv devtools /opt/rh/devtoolset-3
	if (-r ${devtools}) then
#	  if ( -r /tmp/${USER}/setenv.csh) /bin/rm /tmp/${USER}/setenv.csh
#	  scl enable devtoolset-3 $GROUP_DIR/scl_source_STAR > /tmp/${USER}/setenv.csh
#          source /tmp/${USER}/setenv.csh
#          rm /tmp/${USER}/setenv.csh
	  setenv PATH  ${devtools}/root/usr/bin:${PATH}
	  setenv LD_LIBRARY_PATH  ${devtools}/root/usr/lib64:${devtools}/root/usr/lib:${LD_LIBRARY_PATH}
	  setenv INFOPATH  ${devtools}/root/usr/share/info
	  setenv PCP_DIR   ${devtools}/root
          setenv PERL5LIB  ${devtools}/root/usr/lib64/perl5/vendor_perl:${devtools}/root/usr/lib/perl5:${devtools}/root/usr/share/perl5/vendor_perl:${PERL5LIB}
	  setenv PYTHONPATH  ${devtools}/root/usr/lib64/python2.7/site-packages:${devtools}/root/usr/lib/python2.7/site-packages:/opt/rh/devtoolset-3/root/usr/lib64/python2.6/site-packages:/opt/rh/devtoolset-3/root/usr/lib/python2.6/site-packages
	  setenv PKG_CONFIG_PATH  	
	  setenv XDG_CONFIG_DIRS  ${devtools}/root/etc/xdg:/etc/xdg
	  setenv XDG_DATA_DIRS  ${devtools}/root/usr/share:/usr/local/share:/usr/share
          setenv X_SCLS  `basename $devtools`
	else
	  if (-r /afs/rhic.bnl.gov/rcassoft/x8664_sl6/gcc492/bin) then
	    setenv PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl6/gcc492/bin:$PATH
	    if (! $USE_64BITS) then
		setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl6/gcc492/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
#		setenv PREFIX /afs/rhic.bnl.gov/star/packages/.DEV2/.misc/opt/$STAR_HOST_SYS
	    else
		setenv LD_LIBRARY_PATH /afs/rhic.bnl.gov/rcassoft/x8664_sl6/gcc492/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	    endif
          endif
	endif
#        exit 0;
	breaksw
	case "gcc520": # Hongwei private version
	if (-r /opt/gcc/5.2.0)then
	    setenv PATH /opt/gcc/5.2.0/bin:$PATH
	    if (! $USE_64BITS) then
		setenv LD_LIBRARY_PATH /opt/gcc/5.2.0/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	    else
		setenv LD_LIBRARY_PATH /opt/gcc/5.2.0/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	    endif
        else 
          if (-r /star/u/kehw/software/gcc/5.2.0 ) then
	    setenv PATH /star/u/kehw/software/binutils/bin:/star/u/kehw/software/gcc/5.2.0/bin:$PATH
	    if (! $USE_64BITS) then
		setenv LD_LIBRARY_PATH /star/u/kehw/software/binutils/lib:/star/u/kehw/software/gcc/5.2.0/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	    else
		setenv LD_LIBRARY_PATH /star/u/kehw/software/binutils/lib:/star/u/kehw/software/gcc/5.2.0/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	    endif
        endif
	breaksw
	case "gcc521":
	setenv devtools /opt/rh/devtoolset-4
	if (-r ${devtools}) then
	  setenv PATH  ${devtools}/root/usr/bin:${PATH}
	  setenv LD_LIBRARY_PATH  ${devtools}/root/usr/lib64:${devtools}/root/usr/lib:${LD_LIBRARY_PATH}
	  setenv INFOPATH  ${devtools}/root/usr/share/info
	  setenv PCP_DIR   ${devtools}/root
          setenv PERL5LIB  ${devtools}/root/usr/lib64/perl5/vendor_perl:${devtools}/root/usr/lib/perl5:${devtools}/root/usr/share/perl5/vendor_perl:${PERL5LIB}
	  setenv PYTHONPATH  ${devtools}/root/usr/lib64/python2.7/site-packages:${devtools}/root/usr/lib/python2.7/site-packages:/opt/rh/devtoolset-3/root/usr/lib64/python2.6/site-packages:/opt/rh/devtoolset-3/root/usr/lib/python2.6/site-packages
	  setenv PKG_CONFIG_PATH  	
	  setenv XDG_CONFIG_DIRS  ${devtools}/root/etc/xdg:/etc/xdg
	  setenv XDG_DATA_DIRS  ${devtools}/root/usr/share:/usr/local/share:/usr/share
          setenv X_SCLS  `basename $devtools`
	endif
	breaksw
	case "gcc620": # Hongwei private version
	if (-r /opt/gcc/6.2.0)then
	    setenv PATH /opt/gcc/6.2.0/bin:$PATH
	    if (! $USE_64BITS) then
		setenv LD_LIBRARY_PATH /opt/gcc/6.2.0/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	    else
		setenv LD_LIBRARY_PATH /opt/gcc/6.2.0/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	    endif
        endif
	breaksw
	case "gcc710": # Hongwei private version
	if (-r /opt/gcc/7.1.0)then
	    setenv PATH /opt/gcc/7.1.0/bin:$PATH
	    if (! $USE_64BITS) then
		setenv LD_LIBRARY_PATH /opt/gcc/7.1.0/lib:/usr/lib/mysql:$LD_LIBRARY_PATH
	    else
		setenv LD_LIBRARY_PATH /opt/gcc/7.1.0/lib64:/usr/lib64/mysql:$LD_LIBRARY_PATH
	    endif
        endif
	breaksw
	case "gcc447":
	case "gcc":
	breaksw
	default:
	breaksw
      endsw # for specfic gc version
       source ${GROUP_DIR}/.starver $STAR_LEVEL
      if ( $?DECHO ) echo "setup:set PATH = $PATH"
      if ( $?DECHO ) echo "setup:set LD_LIBRARY_PATH = $LD_LIBRARY_PATH"
      if ( $?DECHO ) echo "setup:========== end of gcc set ============="
      breaksw # for all gcc
    # debug / nodebug
    case "nodebug":
    case "NODEBUG":
	unsetenv GPROF
	setenv NODEBUG yes
	 source ${GROUP_DIR}/.starver $STAR_LEVEL
	breaksw
    case "debug":
    case "DEBUG":
	unsetenv NODEBUG
	if ( -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit BIN`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH LIB`
	endif
	 source ${GROUP_DIR}/.starver $STAR_LEVEL
	breaksw


    # grpof / nogrpof
    case "gprof":
    case "GPROF":
	setenv GPROF "yes"
	unsetenv NODEBUG
	 source ${GROUP_DIR}/.starver $STAR_LEVEL
	breaksw

    case "nogprof":
    case "NOGPROF":
	unsetenv GPROF       
	if ( -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit GBIN`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH GLIB`
	endif
	 source ${GROUP_DIR}/.starver $STAR_LEVEL
	breaksw
endsw
if ( $?DECHO ) echo "End of setup ================================"
