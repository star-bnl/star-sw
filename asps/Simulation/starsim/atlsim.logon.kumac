macro logon
alias/create man      'help -n'   c
alias/create opt      option      c
alias/create echo     message     c
alias/create make     gexec       c
alias/create title    title_glob  c
alias/create file     f/file      c
alias/create call     f/call      c
alias/create set      gr/set      c
alias/create more     table/print c
alias/create display  disp        c
alias/create pick     'gr/set txfp -30; gr/set chhe .25; draw/pick'  c 
alias/create trace    'macro/tra off; exec starsim.logon.kumac#trace' c
alias/create nice     starsim.logon.kumac#nice_feature
alias/create set_nice starsim.logon.kumac#set_nice                    c

global/create itrace 0

option stat
option nbox
gr/set stat 111111
filecase keep
* alias/list

** Load support libraries at program execution
LOCAL = $FEXIST('.'//$STAR_HOST_SYS)
IF [LOCAL]=1 THEN
   PREFIX = '.'//$STAR_HOST_SYS//'/lib'
   EXEC load_shared [PREFIX]
ELSE
   EXEC load_shared $STAR_LIB
ENDIF

** setup default production geometry
DETP GEOM y2015a

return


********************************
MACRO load_shared PREFIX
   GEXE [PREFIX]//'/libStarAgmlUtil.so'
RETURN
********************************



*
Macro nice_feature 
  trace off 1   
  if [1] = '?' then
  do i = 2,4
    if $len([%i])=0 .or. [%i]=' '  then
       mess '      > - ' command skipped 
       exitm
    endif
  enddo
  shift
  if ([1] = 'call' .or. [1] = 'gexec') then
* mess call or gexec have only one argument [2]
    [1] [2]
    exitm
  endif
  $unquote([*])
  else
    mess '==>' Unknown command: [*]
  endif
  trace on 1
return 
*
macro set_nice
  trace off 1 
  command 'starsim.logon.kumac#nice_feature $*'
  trace on 1
return
*                      Trace should be the last macro WITHOUT return
Macro TRACE [1]=show [2]=0
   macro/tra off
   import itrace   

   if     [1]=on  then
*     if     [2]>0 then
*       itrace = [itrace]+[2]
*     elseif [itrace]=0 then
       itrace = 1
*     endif
   elseif [1]=off then
     if     [2]>0 then
       itrace = [itrace]-[2]
     elseif [itrace]=1 then
       itrace = 0
     endif
   elseif [1]=show then
     messa itrace = [itrace]   
   else 
     itrace = [1]
   endif
*  messa itrace = [itrace]
   if [itrace].gt.0 then
    sw = on
   else
    sw = off
  endif
  macro/tra [sw]
