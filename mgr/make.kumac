macro make 
on error exitm
filecase KEEP
if [#]>0 then
for pam in [*]
  t = $word([pam],1,1,'/')
  if $index([t],'g2t')>0 .or. $index([t],'gstar')>0 then
    p = pams/sim/[pam]
  elseif $index([t],'pams')<1 then
    p = pams/[pam]
  else
    p = [pam]
  endif 
  exec makes pam=[p]
endfor
else
for pam in geometry  mwc sim/g2t sim/gstar svt vpd ctf ftpc magnet tpc emc global
   exec makes pam=pams/[pam]
endfor
endif
filecase RESTORE
return
**************************************************
macro makes pam=pams/tpc makefile=$STAR/mgr/Makefile
on error exitm
cut = ' \( -type d -a ! -name CVS -a ! -name idl _
                   -a ! -name exa -a ! -name inc _
                   -a ! -name "src/*" _
                   -a ! -name doc _
                   -a ! -name . -a ! -name wrk \) -prune -print'
list = ''
if $fexist([pam])=1 then
list = $shell('find '//[pam]//[cut]);
message [list]
endif
listr = ''
file = $STAR/[pam]
if $fexist([file])=1 then
  listr= $shell('find $STAR/'//[pam]//[cut]);
message [listr]
endif
list_len = $len([list]) + $len([listr])
l1 = [list]
l2 = [listr]
if [list_len]>0 then
  exec load [list] [listr]  
  ami/list
else
  message 'Domain/package' [pam] 'does not exist nor in your privite area neither in standrad one'
endif
return
**************************************************
macro load 
n1 = $words([1],' ')
n2 = $words([2],' ')
i1 = 1 
cwd1 = '????'
if [n1]>0 then
  cwd1 = $word([1],[i1],1)
endif
do i2 = 1, [n2]
  cwd2 = $word([2],[i2],1)
  ind  = $index([cwd2],[cwd1]) 
  if [ind]=0 then
    exec ld_standard [cwd2]
  elseif [i1]<=[n1] then 
    exec ld_private  [cwd1]
    i1 = [i1]+1
    cwd1 = $word([1],[i1],1)
  endif
enddo
do i= [i1], [n1]
  cwd1 = $word([1],[i],1)
  exec ld_private  [cwd1]
enddo 
return
**************************************************
macro ld_standard
  pam = [1]
  n = $words([pam],'/') 
  p = $word([pam],[n],[n],'/')
  slib = $STAR_LIB/[p].sl
  sl = $fexist([slib])
  if [sl]=1 then
     message  '================ Load standard shared library:' [slib] '================'
     for/call  csrmsl($quote([p]))
     gexec [slib]
  endif
return
**************************************************
macro ld_private
  pam = [1]
  n = $words([pam],'/') 
  p = $word([pam],[n],[n],'/')
  nw = $words($STAR_LIB,'/')
  lib = $word($STAR_LIB,[nw],1,'/')
*  wait
  shell gmake NOROOT=YES SILENT=YES -f $STAR/mgr/MakePam.mk -C [pam]
  for/call  csrmsl($quote([p]))
  slib = .$STAR_SYS/[lib]/[p].sl
  sl = $fexist([slib])
  if [sl]=1 then
    message  '================ Load private shared library:' [slib] '================'
    for/call  csrmsl($quote([p]))
    gexec [slib]
  endif
return
endkumac
**************************************************
macro gload
on error exitm
for/call  csrmsl($quote([1]))
*shell gmake [1].sl
for/call  [1]
*gexec [1]
exitm
